#/bin/sh
# BLACK MAGIC to restart with ical (keep the backslash!)        \
exec /usr/bin/ical -f "$0" -nodisplay

# Default path to user's calendar
set defaultpath {/home/$user/.calendar}


# -----------------------------------------------------------------
# Procedure: HTTPHeader
#	Send HTTP header information
# Parameters:
#	none
# Returns:
#	none
# -----------------------------------------------------------------
proc HTTPHeader {sock} {
    puts $sock "Content-type: text/xml"
    puts $sock ""
}

# -----------------------------------------------------------------
# Procedure: StartDocument
#	Display material that appears before the calendar table.
# Parameters:
#	days
# Returns:
#	none
# -----------------------------------------------------------------
proc StartDocument {sock user days} {
    global env

    set acctname $user
    set realname $user
    catch {
	set realname [exec grep ^$acctname: /etc/passwd | awk -F: {{print $5}}]
    }

    puts $sock {<?xml version="1.0" ?>}
    puts $sock "<calendar><days>$days</days>"
}

# -----------------------------------------------------------------
# Procedure: ShowCalendar
#	Display the table of appointments from the ical calendar.
# Parameters:
#       file -- calendar file
#	days -- number of days to show
# Returns:
#	none
# -----------------------------------------------------------------
proc ShowCalendar {sock file days} {
    global ical

    # Generate listing
    calendar cal $file

    cal listing [date today] [expr [date today]+$days-1] i d {
        puts $sock "<item>"
        puts $sock "<date>[date month $d]/[date monthday $d]/[date year $d]</date>"
        if {[$i is appt]} {
            set stime [$i starttime]
            set shour [expr {$stime / 60}]
            set sminute [expr {$stime % 60}]
            puts $sock [format "<starttime>%02d:%02d</starttime>" $shour $sminute]
        }
        puts $sock "<text>[$i text]</text>"
        puts $sock "</item>"
    }
}

# -----------------------------------------------------------------
# Procedure: EndDocument
#	Display material that appears after the calendar display.
# Parameters:
#	none
# Returns:
#	none
# -----------------------------------------------------------------
proc EndDocument {sock} {
    puts $sock "</calendar>"
}


# -----------------------------------------------------------------
# Procedure: HexToChar
#	Convert hex string to a character
# Parameters:
#	hex string
# Returns:
#	char
# -----------------------------------------------------------------
proc HexToChar {hex} {
    scan $hex "%x" val
    return [format "%c" $val]
}

# -----------------------------------------------------------------
# Procedure: CGIParse
#	
# Parameters:
#	
# Returns:
#	
# -----------------------------------------------------------------
proc CGIParse {sock Cgiparms} {
    upvar $Cgiparms cgiparms
    global env
    upvar #0 Httpd$sock data
    
    # Get the encode query string
    set instring $data(query)

    foreach parm [split $instring &] {
        #Httpd_Log $sock CGIParse parm $parm

	# Convert plus's to spaces
	regsub -all {\+} $parm { } parm

	# Convert %XX form to alphanumeric
	regsub -all {\[} $parm {\[} parm
	regsub -all {%([a-eA-E0-9][a-eA-E0-9])} $parm {[HexToChar \1]} parm
	set parm [subst -novariables $parm]

	# Break into key and value
	set eqind [string first = $parm]
	if {$eqind <= 0} {
	    puts stderr "Invalid query encoding"
	    exit 1
	}

	set key [string range $parm 0 [expr $eqind - 1]]
	set val [string range $parm [expr $eqind + 1] end]
	#Httpd_Log $sock CGIParse key $key val $val
	set cgiparms($key) $val
    }
    #parray cgiparms
}

# Simple Sample httpd/1.0 server in 250 lines of Tcl
# Stephen Uhler / Brent Welch (c) 1996 Sun Microsystems
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.

# This is a working sample httpd server written entirely in TCL with the
# CGI and imagemap capability removed.  It has been tested on the Mac, PC
# and Unix.  It is intended as sample of how to write internet servers in
# Tcl. This sample server was derived from a full-featured httpd server,
# also written entirely in Tcl.
# Comments or questions welcome (stephen.uhler@sun.com)

# Httpd is a global array containing the global server state
#  root:	the root of the document directory
#  port:	The port this server is serving
#  listen:	the main listening socket id
#  accepts:	a count of accepted connections so far

array set Httpd {
    -version	"Tcl Httpd-Lite 1.0"
    -launch	0
    -port	8080
    -ipaddr	""
    -default	index.html
    -root	/wwwroot
    -bufsize	32768
    -sockblock	0
    -config	""
}
set Httpd(-host)	[info hostname]

# HTTP/1.0 error codes (the ones we use)

array set HttpdErrors {
    204 {No Content}
    400 {Bad Request}
    404 {Not Found}
    503 {Service Unavailable}
    504 {Service Temporarily Unavailable}
    }


# Start the server by listening for connections on the desired port.

proc Httpd_Server {args} {
    global Httpd

    if {[llength $args] == 1} {
	set args [lindex $args 0]
    }
    array set Httpd $args

#    if {![file isdirectory $Httpd(-root)]} {
#	return -code error "Bad root directory \"$Httpd(-root)\""
#    }
#    if {![file exists [file join $Httpd(-root) $Httpd(-default)]]} {
#	# Try and find a good default
#	foreach idx {index.htm index.html default.htm contents.htm} {
#	    if {[file exists [file join $Httpd(-root) $idx]]} {
#		set Httpd(-default) $idx
#		break
#	    }
#	}
#    }
#    if {![file exists [file join $Httpd(-root) $Httpd(-default)]]} {
#	return -code error "Bad index page \"$Httpd(-default)\""
#    }
    if {$Httpd(-ipaddr) != ""} {
	set Httpd(listen) [socket -server HttpdAccept \
				-myaddr $Httpd(-ipaddr) $Httpd(-port)]
    } else {
	set Httpd(listen) [socket -server HttpdAccept $Httpd(-port)]
    }
    set Httpd(accepts) 0
    if {$Httpd(-port) == 0} {
	set Httpd(-port) [lindex [fconfigure $Httpd(listen) -sockname] 2]
    }
    return $Httpd(-port)
}

# Accept a new connection from the server and set up a handler
# to read the request from the client.

proc HttpdAccept {newsock ipaddr port} {
    global Httpd
    upvar #0 Httpd$newsock data

    incr Httpd(accepts)
    fconfigure $newsock -blocking $Httpd(-sockblock) \
	-buffersize $Httpd(-bufsize) \
	-translation {auto crlf}
    Httpd_Log $newsock Connect $ipaddr $port
    set data(ipaddr) $ipaddr
    fileevent $newsock readable [list HttpdRead $newsock]
}

# read data from a client request

proc HttpdRead { sock } {
    upvar #0 Httpd$sock data

    set readCount [gets $sock line]
    #Httpd_Log $sock readCount $readCount
    #Httpd_Log $sock line $line
    if {![info exists data(state)]} {
	if [regexp {(POST|GET) ([^?]+)\??([^ ]*) HTTP/1.[01]} \
		$line x data(proto) data(url) data(query)] {
	    set data(state) mime
	    Httpd_Log $sock Query $line
	} else {
	    HttpdError $sock 400
	    Httpd_Log $sock Error "bad first line:$line"
	    HttpdSockDone $sock
	}
	return
    }

    # string compare $readCount 0 maps -1 to -1, 0 to 0, and > 0 to 1

    set state [string compare $readCount 0],$data(state),$data(proto)
    #Httpd_Log $sock state $state
    
    switch -- $state {
	0,mime,GET	-
	0,query,POST	{ HttpdRespond $sock }
	0,mime,POST	{ set data(state) query }
	1,mime,POST	-
	1,mime,GET	{
	    if [regexp {([^:]+):[ 	]*(.*)}  $line dummy key value] {
		set data(mime,[string tolower $key]) $value
	    }
	}
	1,query,POST	{
	    set data(query) $line
	    HttpdRespond $sock
	}
	default {
	    if [eof $sock] {
		Httpd_Log $sock Error "unexpected eof on <$data(url)> request"
	    } else {
		Httpd_Log $sock Error "unhandled state <$state> fetching <$data(url)>"
	    }
	    HttpdError $sock 404
	    HttpdSockDone $sock
	}
    }
}

proc HttpdCopyDone { in sock bytes {error ""}} {
#tclLog "CopyDone $sock $bytes $error"
    catch {close $in}
    HttpdSockDone $sock
}
# Close a socket.
# We'll use this to implement keep-alives some day.

proc HttpdSockDone { sock } {
    upvar #0 Httpd$sock data
    unset data
    close $sock
}

# Respond to the query.

proc HttpdRespond { sock } {
    global Httpd
    upvar #0 Httpd$sock data

    #Httpd_Log $sock Respond Query $data(query)

    # ---------- Process the query string ----------
    CGIParse $sock cgiparms

    # ---------- Get the user's identity ----------
    if {[array names cgiparms user] != ""} {
        set user $cgiparms(user)
    } elseif {[info exists env(USER)]} {
        set user $env(USER)
    } elseif {[info exists tcl_platform(user)]} {
        set user $tcl_platform(user)
    } else {
        set user heller
    }
    
    # ---------- Get the calendar file ----------
    set file [subst -nobackslashes -nocommands $::defaultpath]
    if {! [file exists $file]} {
	HttpdError $sock 404
	Httpd_Log $sock Error "$data(url) $in"
        HttpdSockDone $sock
        return
    }

    # ---------- Get the span of days ----------
    set days 10
    if {[array names cgiparms days] != ""} {
        set days $cgiparms(days)
    }

    puts $sock "HTTP/1.0 200 Data follows"
    puts $sock "Date: [HttpdDate [clock seconds]]"
    puts $sock "Server: $Httpd(-version)"

    # ---------- Create the calendar display ----------
    HTTPHeader $sock
    StartDocument $sock $user $days
    ShowCalendar $sock $file $days
    EndDocument $sock
	

    HttpdSockDone $sock
}
# convert the file suffix into a mime type
# add your own types as needed

array set HttpdMimeType {
    {}		text/plain
    .txt	text/plain
    .htm	text/html
    .html	text/html
    .gif	image/gif
    .jpg	image/jpeg
    .xbm	image/x-xbitmap
}

proc HttpdContentType {path} {
    global HttpdMimeType

    set type text/plain
    catch {set type $HttpdMimeType([string tolower [file extension $path]])}
    return $type
}

# Generic error response.

set HttpdErrorFormat {
    <title>Error: %1$s</title>
    Got the error: <b>%2$s</b><br>
    while trying to obtain <b>%3$s</b>
}

proc HttpdError {sock code} {
    upvar #0 Httpd$sock data
    global HttpdErrors HttpdErrorFormat Httpd

    append data(url) ""
    set message [format $HttpdErrorFormat $code $HttpdErrors($code)  $data(url)]
    puts $sock "HTTP/1.0 $code $HttpdErrors($code)"
    puts $sock "Date: [HttpdDate [clock seconds]]"
    puts $sock "Server: $Httpd(-version)"
    puts $sock "Content-Length: [string length $message]"
    puts $sock ""
    puts -nonewline $sock $message
}

# Generate a date string in HTTP format.

proc HttpdDate {clicks} {
    return [clock format $clicks -format {%a, %d %b %Y %T %Z}]
}

# Log an Httpd transaction.
# This should be replaced as needed.

proc Httpd_Log {sock reason args} {
    global httpdLog httpClicks
    if {[info exists httpdLog]} {
	if ![info exists httpClicks] {
	    set last 0
	} else {
	    set last $httpClicks
	}
	set httpClicks [clock seconds]
	set ts [clock format [clock seconds] -format {%Y%m%d %T}]
	puts $httpdLog "$ts ([expr $httpClicks - $last])\t$sock\t$reason\t[join $args { }]"
    }
}

# Convert a url into a pathname.
# This is probably not right.

proc HttpdUrl2File {root url} {
    global HttpdUrlCache Httpd

    if {![info exists HttpdUrlCache($url)]} {
    	lappend pathlist $root
    	set level 0
	foreach part  [split $url /] {
	    set part [HttpdCgiMap $part]
	    if [regexp {[:/]} $part] {
		return [set HttpdUrlCache($url) ""]
	    }
	    switch -- $part {
		.  { }
		.. {incr level -1}
		default {incr level}
	    }
	    if {$level <= 0} {
		return [set HttpdUrlCache($url) ""]
	    } 
	    lappend pathlist $part
	}
    	set file [eval file join $pathlist]
	if {[file isdirectory $file]} {
	    set file [file join $file $Httpd(-default)]
	}
    	set HttpdUrlCache($url) $file
    }
    return $HttpdUrlCache($url)
}

# Decode url-encoded strings.

proc HttpdCgiMap {data} {
    regsub -all {([][$\\])} $data {\\\1} data
    regsub -all {%([0-9a-fA-F][0-9a-fA-F])} $data  {[format %c 0x\1]} data
    return [subst $data]
}

proc bgerror {msg} {
    global errorInfo
    puts stderr "bgerror: $errorInfo"
}
proc openurl url {
   global tcl_platform
   if {[lindex $tcl_platform(os) 1] == "NT"} {
       exec cmd /c start $url &
   } else {
       exec start $url &
   }
}

set httpdLog stderr

upvar #0 Httpd opts

while {[llength $argv] > 0} {
    set option [lindex $argv 0]
    if {![info exists opts($option)] || [llength $argv] == 1} {
	puts stderr "usage: httpd ?options?"
	puts stderr "\nwhere options are any of the following:\n"
	foreach opt [lsort [array names opts -*]] {
	    puts stderr [format "\t%-15s default: %s" $opt $opts($opt)]
	}
	exit 1
    }
    set opts($option) [lindex $argv 1]
    set argv [lrange $argv 2 end]
}

Httpd_Server [array get opts]


puts stderr "Accepting connections on http://$Httpd(-host):$Httpd(-port)/"
set httpClicks [clock seconds]

if {![info exists tcl_service]} {
    vwait forever		;# start the Tcl event loop
}
